import java.util.*;

COMPILER ScrumTaskboard

/* Stefan Schiffer, 29.11.2020 */

/*----------------------------------------------------------
  --- GLOBAL FIELDS AND METHODS
  ----------------------------------------------------------*/

	HashSet<String> setInits = new HashSet<String>();
	HashSet<String> setInitsAll = new HashSet<String>();
	HashMap<String, Float> mapInit = new HashMap<String, Float>();
	HashMap<String, String> allEmployees = new HashMap<String, String>();

    void println(String string) { 
		System.out.println(string);
	}
		
	
/*----------------------------------------------------------
  --- SCANNER
  ----------------------------------------------------------*/

CHARACTERS
    digit =  "0123456789".
    hour1st	= "012".
    min1st = "012345".
    letter =  'a'..'z'. 
    anyButQuote = ANY - '"'.

TOKENS
    initials = letter { letter }.
    string = '"' { anyButQuote } '"'.
    date = digit digit digit digit '-' digit digit '-' digit digit.
    time = hour1st digit ':' min1st digit.
	bezeichnung = digit digit letter.
	kommazahl = digit {digit} ["." digit].
   
COMMENTS FROM "/*" TO "*/" NESTED

IGNORE '\r' + '\n' + '\t'

/*----------------------------------------------------------
  --- PARSER
  ----------------------------------------------------------*/

PRODUCTIONS

ScrumTaskboard = 
	
    "Export" "of" "Taskboard" date time 
    "Developers" (. int devs = 0; .)
	{ Developer (. devs++; .) }
	
	"Bookings" (. int book = 0; .)
	{ Booking (. book++; .) }
	
	(. 
	println(""); 
	println("Das sind " + devs + " Entwickler/innen."); 
	println("Das sind " + book + " Bookings."); 
	println("");
	println("Diese Entwickler haben am Sprint teilgenommen: " + setInits.toString());
	setInitsAll.removeAll(setInits);
	println("Diese Entwickler haben nicht am Sprint teilgenommen: " + setInitsAll.toString());
	
	println("Diese Stunden haben die Entwickler gearbeitet: " + mapInit.toString());
	.)	
	.

Developer = 
    Initials<^String initials> 
	(. setInitsAll.add(initials); .)
    Name<^String name>
	(. allEmployees.put(initials, name); .)
	.
		
Initials<^String initials> =
	initials (. initials = t.val; .)
	.
	
Name<^String name> =
    string (. name = t.val; .)
	.	
		
Booking= 
	Bezeichnung<^String bezeichnung>
	Inhalt {Inhalt}
	.
	
Bezeichnung<^String bezeichnung> =
	 bezeichnung (. bezeichnung = t.val; .)
	.	
	
Inhalt = 
	Date<^String date>
	Kommazahl<^float kommazahl1>
	WorkDone {WorkDone}
	.

WorkDone= 
	Initials<^String initials> 
	Kommazahl<^float kommazahl2>
	(.
	String init = initials;
	setInits.add(init);
	
	if(mapInit.containsKey(init)){
		mapInit.put(init, mapInit.get(init)+kommazahl2);
	}else{
		mapInit.put(init, kommazahl2);
	}
	.)
	.

Date<^String date> = 
	date (. date = t.val; .)
	.
	
Kommazahl<^float kommazahl> =
	kommazahl (. kommazahl = Float.valueOf(t.val); .)
	.

END ScrumTaskboard.
